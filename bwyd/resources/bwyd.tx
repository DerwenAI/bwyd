Module:
  cites*=Cite
  posts*=Post
  closures+=Closure
;

Cite:
  "CITE:" url=STRING
;

Post:
  "POST:" url=STRING
;

Closure:
  "CLOSURE:" name=STRING
    ("TITLE:" title=STRING)?
    ("NOTE:" note=STRING)?
    steps+=Step
  "YIELDS" yields=Measure
;

Step:
  Ratio | Header | Container | Use | Ingredient | Focus | Tool | Add | Action | Bake | Chill
;

Ratio:
  "RATIO:" name=STRING "=" (parts+=RatioPart[','])
;

RatioPart:
    symbol=ID ('[' components+=ID[','] ']')?
;

Header:
  "HEADER:" text=STRING
;

Container:
  "CONTAINER" symbol=ID ":" text=STRING
;

Use:
  "USE" symbol=ID ":" text=STRING
;

Ingredient:
  "INGREDIENT" symbol=ID ":" text=STRING
;

Focus:
  "FOCUS" symbol=ID
;

Tool:
  "TOOL" symbol=ID ":" text=STRING
;

Add:
  "ADD" symbol=ID measure=Measure (":" text=STRING)?
;

Measure:
  "(" amount=NUMBER (units=MeasureUnits)? ")"
;

MeasureUnits:
  "g" | "kg" | "ml" | "l"
;

Action:
  "ACTION" symbol=ID (":" modifier=STRING)?
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

Duration:
  "(" amount=NUMBER units=DurationUnits ")"
;

DurationUnits:
  "sec" | "min" | "hrs"
;

Bake:
  OvenMode symbol=ID (":" modifier=STRING)?
    "AT:" temperature=Temperature
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

OvenMode:
  "BAKE" | "BROIL" | "ROAST" | "TOAST"
;

Temperature:
  amount=NUMBER units=TemperatureUnits
;

TemperatureUnits:
  "C"
;

Chill:
  "CHILL" symbol=ID (":" modifier=STRING)?
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

Comment:
  /\/\/.*$/
;
