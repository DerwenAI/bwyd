Module:
  "TITLE:" title=STRING
  "TEXT:" text=STRING
  meta+=Module_Metadata
  closures+=Closure
;

Module_Metadata:
  Author | Updated | License | Post | Cite
;

Author:
  "AUTHOR:" name=STRING
;

Updated:
  "UPDATED:" date=STRING
;

License:
  "LICENSE:" spdx_id=STRING
;

Cite:
  "CITE:" url=STRING
;

Post:
  "POST:" url=STRING
;

Closure:
  "CLOSURE:" name=STRING ("AS" supers=IDList)?
    ("TEXT:" text=STRING)?
    ("KEYWORDS:" keywords=IDList)?
    depend+=Dependency
    foci+=Focus
    ratio=Ratio?
  prods+=Yield
;

IDList: ids+=ID[","];

Yield:
"YIELDS" symbol=ID measure=Measure (intermediate="INTERMEDIATE")?
;

Dependency:
  Container | Tool | Ingredient | Use
;

Container:
  "CONTAINER" symbol=ID ":" text=STRING
;

Tool:
  "TOOL" symbol=ID ":" text=STRING
;

Ingredient:
  "INGREDIENT" symbol=ID ":" text=STRING
;

Use:
  "USE" symbol=ID ":" text=STRING
;

Focus:
  "FOCUS" symbol=ID
  activities+=Activity
;

Activity:
  "ACTIVITY:" text=STRING
   ops+=Op
;

Op:
  Note | Transfer | Add | Action | Store | Heat | Chill | Bake
;

Note:
  "NOTE:" text=STRING
;

Transfer:
  "TRANSFER" symbol=ID
;

Add:
  "ADD" symbol=ID measure=Measure (":" text=STRING)?
;

Action:
  "ACTION" symbol=ID (":" modifier=STRING)?
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

Store:
  "STORE" symbol=ID (":" modifier=STRING)?
     "UPTO" duration=Duration
;

Heat:
  "HEAT" symbol=ID (":" modifier=STRING)?
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

Chill:
  "CHILL" symbol=ID (":" modifier=STRING)?
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

Bake:
  OvenMode symbol=ID (":" modifier=STRING)?
    "AT:" temperature=Temperature
    "UNTIL:" until=STRING
    "TIME" duration=Duration
;

OvenMode:
  "BAKE" | "BROIL" | "ROAST" | "TOAST"
;

Measure:
  "(" amount=NUMBER (units=MeasureUnits)? ")"
;

MeasureUnits:
  "g" | "kg" | "ml" | "l"
;

Duration:
  "(" amount=NUMBER units=DurationUnits ")"
;

DurationUnits:
  "second" | "minute" | "hour" | "day" | "month" | "year"
;

Temperature:
  amount=NUMBER units=TemperatureUnits
;

TemperatureUnits:
  "C"
;

Ratio:
  "RATIO:" name=STRING "=" (parts+=RatioPart[','])
;

RatioPart:
    symbol=ID ('[' components+=ID[','] ']')?
;

Comment:
  /\/\/.*$/
;
