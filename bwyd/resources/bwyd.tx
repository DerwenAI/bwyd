Program:
  closures+=Closure
;

Closure:
  "CLOSURE:" name=STRING
    steps+=Step
  "YIELDS" yields=Measure
;

Step:
  Ratio | Note | Container | Use | Ingredient | Focus | Tool | Add | Action | Chill
;

Ratio:
  "RATIO:" name=STRING "=" (elements+=RatioElement[','])
;

RatioElement:
    symbol=ID ('[' components+=ID[','] ']')?
;

Note:
  "NOTE:" text=STRING
;

Container:
  "CONTAINER" symbol=ID ":" text=STRING
;

Use:
  "USE" symbol=ID ":" name=STRING
;

Ingredient:
  "INGREDIENT" symbol=ID ":" text=STRING
;

Focus:
  "FOCUS" symbol=ID
;

Tool:
  "TOOL" symbol=ID ":" text=STRING
;

Add:
  "ADD" symbol=ID measure=Measure (":" text=STRING)?
;

Measure:
  "(" amount=NUMBER (units=MeasureUnits)? ")"
;

MeasureUnits:
  "g" | "kg" | "ml" | "l"
;

Action:
  "ACTION" symbol=ID (":" modifier=STRING)? "UNTIL:" until=STRING "TIME" duration=Duration
;

Duration:
  "(" amount=NUMBER units=DurationUnits ")"
;

DurationUnits:
  "sec" | "min" | "hrs"
;

Chill:
  "CHILL" symbol=ID (":" modifier=STRING)? "UNTIL:" until=STRING "TIME" duration=Duration
;

Comment:
  /\/\/.*$/
;
